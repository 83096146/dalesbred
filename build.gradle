buildscript {
    ext {
        kotlinVersion = '1.0.6'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.2'
    id 'pl.allegro.tech.build.axion-release' version '1.5.0'
}

apply plugin: 'io.codearte.nexus-staging'

ext {
    springVersion = '4.3.6.RELEASE'
    junitVersion = '4.12'
    jetbrainsAnnotationsVersion = '15.0'
    hsqldbVersion = '2.3.4'
    logbackVersion = '1.2.1'
}

scmVersion {
    localOnly = true
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

configure(allprojects) { project ->
    apply plugin: 'base'
    group = 'org.dalesbred'
    project.version = scmVersion.version

    repositories {
        jcenter()
    }
}

asciidoctor {
    sourceDir = file('src/asciidoc')
    options = [
            eruby: 'erubis'
    ]
    attributes = [
            icons: 'font',
            toc: 'right',
            'source-highlighter': 'prettify',
            revnumber: project.version.toString(),
            docinfo: "",
            javadocBase: "http://dalesbred.org/docs/api/?"
    ]
}

task publish {
    description 'Publishes both the artifacts and the website'

    dependsOn ':website:publishGhPages',
            ':dalesbred:uploadArchives',
            ':dalesbred-junit:uploadArchives'
}

configure([project(':dalesbred'), project(':dalesbred-junit')]) {
    apply plugin: 'java'
    apply plugin: 'com.bmuschko.nexus'
    apply from: "${rootProject.projectDir}/gradle/publish-maven.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    javadoc {
        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        options.header = project.name
        options.links('http://docs.oracle.com/javase/8/docs/api/',
                'http://docs.spring.io/spring/docs/current/javadoc-api/',
                'http://www.joda.org/joda-time/apidocs/')
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}
