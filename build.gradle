buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.2'
    id 'org.ajoberstar.github-pages' version '1.1.0'
    id 'pl.allegro.tech.build.axion-release' version '1.2.4'
}

scmVersion {
    localOnly = true
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

configure(allprojects) { project ->
    group = 'org.dalesbred'
    project.version = scmVersion.version

    repositories {
        jcenter()
    }
}

configure(rootProject) { project ->
    asciidoctor {
        sourceDir = file('src/asciidoc')
        options = [
                eruby: 'erubis'
        ]
        attributes = [
                icons: 'font',
                toc: 'right',
                'source-highlighter': 'prettify',
                revnumber: project.version.toString(),
                docinfo: "",
                javadocBase: "http://dalesbred.org/docs/api/?"
        ]
    }

    task publish {
        description 'Publishes both the artifacts and the website'
    }
}

configure(subprojects) { project ->
    apply plugin: 'com.bmuschko.nexus'
    apply from: "${rootProject.projectDir}/gradle/publish-maven.gradle"

    ext.springVersion = '4.1.6.RELEASE'
    ext.junitVersion = '4.12'
    ext.jetbrainsAnnotationsVersion = '13.0'
    ext.hsqldbVersion = '2.3.2'
}

configure([project(':dalesbred'), project(':dalesbred-junit')]) {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    publish.dependsOn uploadArchives

    javadoc {
        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        options.header = project.name
        options.links('http://docs.oracle.com/javase/8/docs/api/',
                'http://docs.spring.io/spring/docs/current/javadoc-api/',
                'http://www.joda.org/joda-time/apidocs/')
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

project(':dalesbred') {
    description = 'Dalesbred - a database access library'

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.14'
        compile 'joda-time:joda-time:2.7'
        compile 'org.threeten:threetenbp:1.2'
        compile "org.springframework:spring-context:$springVersion"
        compile "org.springframework:spring-jdbc:$springVersion"
        compile "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"

        compile 'org.postgresql:postgresql:9.4-1201-jdbc41'

        testCompile "org.hsqldb:hsqldb:$hsqldbVersion"
        testCompile "com.h2database:h2:1.4.190"
        testCompile 'mysql:mysql-connector-java:5.1.35'
        testCompile "junit:junit:$junitVersion"
        testCompile 'org.mockito:mockito-core:1.10.19'
        testCompile 'ch.qos.logback:logback-core:1.1.3'
        testCompile 'ch.qos.logback:logback-classic:1.1.3'
    }

    apply plugin: 'osgi'

    jar {
        manifest {
            instruction 'Import-Package', '' +
                    'javax.inject.*;resolution:=optional,' +
                    'org.joda.time.*;resolution:=optional,' +
                    'org.springframework.*;resolution:=optional,' +
                    'org.postgresql.*;resolution:=optional,' +
                    '*'
        }
    }
}

project(':dalesbred-junit') {
    description = 'Dalesbred JUnit-support'

    dependencies {
        compile project(':dalesbred')
        compile "junit:junit:$junitVersion"
        compile 'javax.inject:javax.inject:1'

        compile "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"

        testCompile "org.hsqldb:hsqldb:$hsqldbVersion"
        testCompile 'ch.qos.logback:logback-core:1.1.3'
        testCompile 'ch.qos.logback:logback-classic:1.1.3'
    }
}

project(':website') {

    task copySources(type: Copy) {
        from 'src'
        into "build/dalesbred"
    }

    task copyApi(type: Copy) {
        from files(tasks.findByPath(':dalesbred:javadoc'))
        into "build/dalesbred/docs/api/"
    }

    task copyReference(type: Copy) {
        dependsOn tasks.findByPath(':asciidoctor')
        from '../build/asciidoc/html5/'
        into "build/dalesbred/docs/reference/"
    }

    assemble.dependsOn copySources
    assemble.dependsOn copyApi
    assemble.dependsOn copyReference

    publishGhPages.dependsOn assemble
    publish.dependsOn publishGhPages

    githubPages {
        repoUri = 'git@github.com:EvidentSolutions/dalesbred.git'
        deleteExistingFiles = true
        pages {
            from files('build/dalesbred')
        }
    }
}
