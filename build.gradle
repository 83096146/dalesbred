buildscript {
    repositories {
        maven { url 'http://repo.springsource.org/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.build.gradle:propdeps-plugin:0.0.5")
    }
}

configure(allprojects) { project ->
    group = 'fi.evident.dalesbred'
    version = '0.6.1-SNAPSHOT'
}

configure(subprojects) { project ->
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-maven'

    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
    ext.springVersion = '3.2.4.RELEASE'
    ext.junitVersion = '4.11'
    ext.jetbrainsAnnotationsVersion = '13.0'
    ext.hsqldbVersion = '2.3.1'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar

        archives javadocJar
        archives sourceJar
    }

    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "file:${rootProject.projectDir}/build/repo")
                // repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                //repository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                //    authentication(userName: sonatypeUsername, password: sonatypePassword)
                //}

                pom.whenConfigured { pom ->
                    pom.project {
                        name project.name
                        packaging 'jar'
                        description project.description
                        url 'https://bitbucket.org/evidentsolutions/dalesbred'
                        inceptionYear '2012'

                        organization {
                            name 'Evident Solutions'
                            url 'http://www.evident.fi'
                        }

                        licenses {
                            license {
                                name 'MIT License'
                                url 'http://opensource.org/licenses/mit-license.php'
                                distribution 'repo'
                            }
                        }

                        scm {
                            url 'https://bitbucket.org/evidentsolutions/dalesbred'
                            connection 'scm:git:https://bitbucket.org/evidentsolutions/dalesbred.git'
                            developerConnection 'scm:git:ssh://git@bitbucket.org/evidentsolutions/dalesbred.git'
                        }

                        developers {
                            developer {
                                id 'komu'
                                name 'Juha Komulainen'
                            }
                        }

                        issueManagement {
                            system = 'Bitbucket'
                            url = 'https://bitbucket.org/evidentsolutions/dalesbred/issues'
                        }
                    }

                    // Test dependencies are not needed in pom
                    pom.dependencies.removeAll { dep ->
                        dep.scope == "test"
                    }
                }
            }
        }
    }
}

project(':dalesbred') {
    description = 'Dalesbred'

    dependencies {
        optional 'joda-time:joda-time:2.3'
        optional 'aopalliance:aopalliance:1.0'
        optional 'com.google.inject:guice:3.0'
        optional "org.springframework:spring-context:$springVersion"
        optional "org.springframework:spring-jdbc:$springVersion"
        optional "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"

        optional 'org.postgresql:postgresql:9.3-1100-jdbc41'

        testCompile "org.hsqldb:hsqldb:$hsqldbVersion"
        testCompile 'mysql:mysql-connector-java:5.1.26'
        testCompile "junit:junit:$junitVersion"
        testCompile 'org.mockito:mockito-core:1.9.5'
    }
}

project(':dalesbred-junit') {
    description = 'Dalesbred JUnit-support'

    dependencies {
        compile project(':dalesbred')
        compile "junit:junit:$junitVersion"
        compile 'javax.inject:javax.inject:1'

        optional "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"

        testCompile "org.hsqldb:hsqldb:$hsqldbVersion"
    }
}
