buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'net.researchgate.release' version '2.0.2'
    id 'org.asciidoctor.convert' version '1.5.2'
    id 'org.hidetake.ssh' version '1.1.1'
}

release {
    // tagTemplate = 'v$version' // TODO: this is not available in release-plugin 2.0.2 but we want to use it later
}

remotes {
    website {
        host = 'uuhi.evident.fi'
        user = 'evident'
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
}

configure(allprojects) { project ->
    group = 'fi.evident.dalesbred'

    apply plugin: 'java'

    task buildDocs {
        group 'documentation'
        description 'Builds all documentation.'

        dependsOn javadoc
    }

    repositories {
        mavenCentral()
    }
}

configure(rootProject) { project ->
    buildDocs.dependsOn asciidoctor

    asciidoctor {
        sourceDir = file('src/asciidoc')
        options = [
                eruby: 'erubis'
        ]
        attributes = [
                icons: 'font',
                toc: 'right',
                'source-highlighter': 'prettify',
                revnumber: project.version.toString(),
                docinfo: "",
                javadocBase: "https://dalesbred.evident.fi/docs/${project.version.toString()}/api/?"
        ]
    }

    task publishWebsite {
        group 'publishing'
        description 'Publishes the website to dalesbred.evident.fi'

        doLast {
            ssh.run {
                session(remotes.website) {
                    put from: "${project.projectDir}/website/src", into: "/var/www/dalesbred"
                }
            }
        }
    }

    task publishReferenceDocumentation {
        group 'publishing'
        description 'Publishes the reference documentation to dalesbred.evident.fi'
        dependsOn buildDocs

        doLast {
            ssh.run {
                session(remotes.website) {
                    def target = "/var/www/dalesbred/docs/${project.version}/reference"
                    execute "mkdir -p $target"
                    put from: "${project.projectDir}/build/asciidoc/html5", into: target
                }
            }
        }
    }

    task updateCurrentDocsSymlink {
        group 'publishing'
        description 'Updates the symlink of current docs to point to this version'

        doLast {
            ssh.run {
                session(remotes.website) {
                    execute "ln -sfT /var/www/dalesbred/docs/${project.version} /var/www/dalesbred/docs/current"
                }
            }
        }
    }
}

configure(subprojects) { project ->
    apply plugin: 'com.bmuschko.nexus'
    apply from: "${rootProject.projectDir}/gradle/publish-maven.gradle"

    project.version = rootProject.version.toString()
    buildDocs.dependsOn javadoc

    ext.isReleaseVersion = { !version.endsWith("SNAPSHOT") }
    ext.springVersion = '4.0.3.RELEASE'
    ext.junitVersion = '4.11'
    ext.jetbrainsAnnotationsVersion = '13.0'
    ext.hsqldbVersion = '2.3.2'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    javadoc {
        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        options.header = project.name
        options.links('http://docs.oracle.com/javase/8/docs/api/',
                'http://docs.spring.io/spring/docs/current/javadoc-api/',
                'http://www.joda.org/joda-time/apidocs/')
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

project(':dalesbred') {
    description = 'Dalesbred - a database access library'

    dependencies {
        compile 'joda-time:joda-time:2.3'
        compile 'org.threeten:threetenbp:1.0'
        compile 'aopalliance:aopalliance:1.0'
        compile 'com.google.inject:guice:3.0'
        compile "org.springframework:spring-context:$springVersion"
        compile "org.springframework:spring-jdbc:$springVersion"
        compile "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"

        compile 'org.postgresql:postgresql:9.3-1101-jdbc41'

        testCompile "org.hsqldb:hsqldb:$hsqldbVersion"
        testCompile 'mysql:mysql-connector-java:5.1.30'
        testCompile "junit:junit:$junitVersion"
        testCompile 'org.mockito:mockito-core:1.9.5'
    }

    apply plugin: 'osgi'

    jar {
        manifest {
            instruction 'Import-Package', '' +
                    'com.google.inject.*;resolution:=optional,' +
                    'javax.inject.*;resolution:=optional,' +
                    'org.aopalliance.intercept.*;resolution:=optional,' +
                    'org.joda.time.*;resolution:=optional,' +
                    'org.springframework.*;resolution:=optional,' +
                    'org.postgresql.*;resolution:=optional,' +
                    '*'
        }
    }

    task publishJavadocs {
        group 'publishing'
        description 'Publishes the javadocs documentation to dalesbred.evident.fi'
        dependsOn javadoc

        doLast {
            ssh.run {
                session(remotes.website) {
                    def target = "/var/www/dalesbred/docs/${project.version}/api"
                    execute "mkdir -p $target"
                    put from: "${project.projectDir}/build/docs/javadoc", into: target
                }
            }
        }
    }
}

project(':dalesbred-junit') {
    description = 'Dalesbred JUnit-support'

    dependencies {
        compile project(':dalesbred')
        compile "junit:junit:$junitVersion"
        compile 'javax.inject:javax.inject:1'

        compile "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"

        testCompile "org.hsqldb:hsqldb:$hsqldbVersion"
    }
}
