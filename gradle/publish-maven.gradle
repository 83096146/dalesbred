uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (project.hasProperty('sonatypeUsername')) {
                def repo = version.endsWith("SNAPSHOT") ? "https://oss.sonatype.org/content/repositories/snapshots/" : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

                repository(url: repo) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }

            pom.whenConfigured { pom ->
                pom.project {
                    name project.name
                    packaging 'jar'
                    description project.description
                    url 'https://dalesbred.evident.fi/'
                    inceptionYear '2012'

                    organization {
                        name 'Evident Solutions'
                        url 'http://www.evident.fi'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://opensource.org/licenses/mit-license.php'
                            distribution 'repo'
                        }
                    }

                    scm {
                        url 'https://bitbucket.org/evidentsolutions/dalesbred'
                        connection 'scm:git:https://bitbucket.org/evidentsolutions/dalesbred.git'
                        developerConnection 'scm:git:ssh://git@bitbucket.org/evidentsolutions/dalesbred.git'
                    }

                    developers {
                        developer {
                            id 'komu'
                            name 'Juha Komulainen'
                        }
                    }

                    issueManagement {
                        system = 'Bitbucket'
                        url = 'https://bitbucket.org/evidentsolutions/dalesbred/issues'
                    }
                }

                // Test dependencies are not needed in pom
                pom.dependencies.removeAll { dep ->
                    dep.scope == "test"
                }

                // Mark all other dependencies as optional
                for (dep in pom.dependencies)
                    dep.optional = true
            }
        }
    }
}
